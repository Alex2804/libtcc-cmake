
if(__define_libtcc_definitions)
    return()
endif()
set(__define_libtcc_definitions INCLUDED)

function(define_libtcc_definitions __TARGET_ARCHITECTURE__ __ARM_EABI__ __HAS_VFP__ __HAS_HARDFLOAT__ __LIBTCC_COMPILE_DEFINITIONS__ __STATUS_MESSAGE__)
    if(DEFINED ${__LIBTCC_COMPILE_DEFINITIONS__})
        list(APPEND __DEFINITIONS__ "${${__LIBTCC_COMPILE_DEFINITIONS__}}")
    endif()

    list(APPEND __DEFINITIONS__ TCC_LIBTCC1="libtcc1-${LIBTCC_SUFFIX}.a")
    list(APPEND __DEFINITIONS__ TCC_TARGET_${${__TARGET_ARCHITECTURE__}})

    if(LIBTCC_ONE_SOURCE)
        list(APPEND __DEFINITIONS__ ONE_SOURCE=1)
    else()
        list(APPEND __DEFINITIONS__ ONE_SOURCE=0)
    endif()

    if(WIN32)
        list(APPEND __DEFINITIONS__ "TCC_TARGET_PE")
        if("${LIBTCC_BUILD_TYPE}" STREQUAL "SHARED")
            list(APPEND __DEFINITIONS__ "LIBTCC_AS_DLL")
        endif()
    elseif(APPLE)
        if(NOT "${${__TARGET_ARCHITECTURE__}}" STREQUAL "X86_64")
            message(FATAL_ERROR "Libtcc only supports x86_64 on macOS X!")
        endif()
        list(APPEND __DEFINITIONS__ "TCC_TARGET_MACHO")
    elseif(UNIX)
        string(TOLOWER ${CMAKE_SYSTEM_NAME} LIBTCC_CONFIG_TRIPLET_SYSTEM_NAME)
        if(${__ARM_EABI__})
            if(${__HAS_HARDFLOAT__})
                set(LIBTCC_CONFIG_TRIPLET_ABI gnueabihf)
            else()
                set(LIBTCC_CONFIG_TRIPLET_ABI gnueabi)
            endif()
        else()
            set(LIBTCC_CONFIG_TRIPLET_ABI gnu)
        endif()
        set(LIBTCC_CONFIG_TRIPLET "\"${LIBTCC_SUFFIX}-${LIBTCC_CONFIG_TRIPLET_SYSTEM_NAME}-${LIBTCC_CONFIG_TRIPLET_ABI}\"")
        list(APPEND __DEFINITIONS__ "CONFIG_TRIPLET=${LIBTCC_CONFIG_TRIPLET}")
    endif()

    if(${__ARM_EABI__})
        list(APPEND __DEFINITIONS__ TCC_ARM_EABI)
    endif()
    if(${__HAS_VFP__})
        list(APPEND __DEFINITIONS__ TCC_ARM_VFP)
    endif()
    if(${__HAS_HARDFLOAT__})
        list(APPEND __DEFINITIONS__ TCC_ARM_HARDFLOAT)
    endif()

    if(${__STATUS_MESSAGE__})
        message(STATUS "Libtcc compile definitions: ${__DEFINITIONS__}")
    endif()
    set(${__LIBTCC_COMPILE_DEFINITIONS__} "${__DEFINITIONS__}" PARENT_SCOPE)
endfunction()